TML {

Exp
  = TMLLocalizedStrings
 
// Comments
comment
  = "//" (~"\n" any)* --single
  | "/*" (~"*/" any)* "*/" --multiple
  
space
  += comment

TMLLocalizedStrings
  = ((~TMLLocalizedString any)* TMLLocalizedString)+ (~TMLLocalizedString any)*


// TMLLocalizedString* expression
TMLLocalizedString
  = Macro "(" StringLiteral ("," ListOf<Arg, ",">)* ")"

  
Macro
  = "TMLLocalizedString"
  | "TMLLocalizedAttributedString"
  | "TMLLocalizedDate"
  | "TMLLocalizedAttributedDate"


// TMLLocalized argument expression
NamedArg
  = Variable ":" Arg
  
Arg
  = Null | Literal | Object | CollectionAccess | Property | Variable


// Null Expression
Null
  = "nil"
  | "NSNull()"

CollectionAccess
  = Property "[" Arg "]"
  | Variable "[" Arg "]"
  
Property
  = Variable ("." Variable)*

Variable
  = #(validVarChar+)
  
validVarChar
  = ~( "[" | "]" | ":" | "," | "." | ")" | "(" | space) any

// Literal Objects
Literal
  = NumberLiteral
  | StringLiteral
  
Object
  = Dict
  | Array
  

// Numbers
NumberLiteral
  = digit+

// Strings
StringLiteral
  = ("\"" stringChars "\"")+

stringChars
  = stringChar*

stringChar
  = "\\\""
  | ~"\"" any

// Dictionary
Dict
  = "[" ":" "]"
  | "[" ListOf<DictEntry, ","> "]"
  
DictEntry
  = StringLiteral ":" DictValue
  
DictValue
  = Arg


// Array
Array
  = "[" ListOf<ArrayEntry, ","> "]"

ArrayEntry
  = Arg

}