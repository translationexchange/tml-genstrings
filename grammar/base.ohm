Base {

// Invocation expression
Exp
  = TMLLocalizedStrings
 
// Comments
comment
  = "//" (~"\n" any)* --single
  | "/*" (~"*/" any)* "*/" --multiple
  
// Treat comments as space
space
  += comment

// All localized strings, consuming entire input
TMLLocalizedStrings
  = ((~TMLLocalizedString any)* TMLLocalizedString)+ (~TMLLocalizedString any)*
  
// Localized string (default treats any string as localized, sub-grammars must override)
TMLLocalizedString
  = StringLiteral
  
  
// Literals
Literal
  = NumberLiteral
  | StringLiteral
  | BooleanLiteral
  | NullLiteral
  
NullLiteral
  = "null"
  
BooleanLiteral
  = "true"
  | "false"

NumberLiteral
  = digit+ ("." digit+)?
  
stringNonLiteral
(~"" nothing)
  = ~""
  
StringLiteral
  = QuotedStringLiteral<"\"", stringNonLiteral>
  | QuotedStringLiteral<"'", stringNonLiteral>
  
QuotedStringLiteral<quot, special>
  = quot quotedStringChars<(quot | special)> quot
  
quotedStringChars<special>
  = quotedChar<special>+
  
quotedChar<special>
  = "\\" special --escaped
  | ~special any
  
  
// Variables
Variable
  = #(validFirstVarChar validVarChar*)

validFirstVarChar
  = ~( "[" | "]" | ":" | "," | "." | ")" | "(" | "}" | "{" | "\"" | "'" | digit | space) any
  
validVarChar
  = ~( "[" | "]" | ":" | "," | "." | ")" | "(" | "}" | "{" | "\"" | "'" | space) any
  
// Operators
operator
  = unaryOperator
  | binaryOperator
  | ternaryOperator

unaryOperator
  = ("!" | "+" | "-" | "*" | "&" | "~")+
  
binaryOperator
  = ("&" | "|" | "^" | "<" | ">" | "+" | "-" | "/" | "%" | "*" | "=")+
  | "and" | "or" | "xor"
  
ternaryOperator
  = "?" | ":"
  
  
// Arguments
Arg
  = Method | Property | Variable | Literal
  
Method
  = MethodNamed<MethodName>
  
MethodNamed<methodName>
  = methodName "(" ListOf<ArgExp, ","> ")"
  
MethodName
  = Variable
  
Property
  = ArgExp ("." ArgExp)+
  
ArgExp
  = "(" ArgExp ")" --parens
  | ArgExp ternaryOperator ArgExp ternaryOperator ArgExp --ternary
  | ArgExp binaryOperator ArgExp --binary
  | unaryOperator ArgExp --unary
  | ArgExp unaryOperator --unaryAfter
  | Arg
  
  
// HTML
HTMLAnyTag
  = "<" HTMLTagName HTMLTagAttributeExp* "/"? ">"
  
HTMLCloseAnyTag
  = "<" "/" HTMLTagName ">"
  
HTMLTag<tag>
  = "<" tag HTMLTagAttributeExp* "/"? ">"
  
HTMLCloseTag<tag>
  = "<" "/" tag ">"
  
HTMLTagWithAttribute<tag, attr>
  = "<" tag (~(attr | ">") HTMLTagAttributeExp)* attr HTMLTagAttributeExp* "/"? ">"
  
HTMLTagAttributeExp
  = HTMLTagAttributeName ("=" HTMLTagAttributeValue)?
  
HTMLTagWithContent<tag, content>
  = HTMLTag<tag> content HTMLCloseTag<tag>
  
HTMLTagWithAttributeAndContent<tag, attr, content>
  = HTMLTagWithAttribute<tag, attr> content HTMLCloseTag<tag>
  
HTMLTagName
  = #(alnum+)
  
HTMLTagAttributeName
  = #(htmlAttributeChar+)
  
HTMLTagAttributeValue
  = StringLiteral
  
htmlAttributeChar
  = ~( space | "\"" | "'" | ">" | "/" | "=" ) any
  
}