Base {

Tokens (default rule - we extract tokens)
  = token+
  
macro (defines macro)
  = "tr"
  
dynamicMacro (dynamic macros are to be ignored, only certain languages have those, like php)
  = end any
  
token
  = dynamicMacro | macro | literal | openExpressionOperator | closeExpressionOperator | namedToken | operator 
  
literal
  = interpolatedString | string | number
  
stringQuote
  = "\"" | "'"
  
stringConcatOperator
  = end any // no default value
  
string
  = string (space* stringConcatOperator space* string)+ --concat
  | quotedString<"\""> 
  | quotedString<"'"> 
  
stringInterpolationStart
  = end any // no default value
  
stringInterpolationEnd
  = end any // no default value
  
stringInterpolation
  = stringInterpolationStart (stringInterpolationToken ~stringInterpolationEnd)* (stringInterpolationToken &stringInterpolationEnd)* stringInterpolationEnd --default
  
stringInterpolationToken
  = "(" stringInterpolationToken ")" --exp
  | token
  | space
  
interpolatedString
  = stringQuote ((~stringInterpolationStart quotedChar<stringQuote>)* stringInterpolation)+ quotedChar<stringQuote>* stringQuote
  
quotedString<quot>
  = quot quotedChar<quot>* quot
  
quotedChar<special>
  = "\\" special --escaped
  | ~special any
  
number
  = hexNumber | octalNumber | expNumber | decimal
  
hexNumber
  = "-"? "0x" hexDigit+
  
octalNumber
  = "-"? "0" digit+
  
expNumber
  = decimal ("e"|"E") "-"? digit+
  
decimal
  = digit* "." digit+ --float
  | digit+
  
  
operator
  = runinOperator+ 
  | "." | "," | ":" | ";" | "{" | "}" | "(" | ")" | "[" | "]" | "<" | ">" | "=" | "?" | "-" | "+" | "/" | "*" | "~" | "%" | "@"
  
runinOperator
  = "." | "<" | ">" | "=" | "-" | "+" | "/" | "*"
  
openExpressionOperator
  = "{" | "(" | "["
  
closeExpressionOperator
  = "}" | ")" | "]"
  
space
  += comment
  
comment
  = trailingComment
  | multilineComment
  
namedToken
  = (~(string | operator | space | stringInterpolationStart | stringInterpolationEnd) any)+
  
trailingComment
  = "//" (~"\n" any)*
  
multilineComment
  = "/*" (~"*/" any) "*/"
  
  
  
// TRL Support

trlQuote
  = "\"" | "'" | "&quot;"
  
trlString
  = quotedString<trlQuote> 

quotedTRLExp
  = trlQuote trlExp trlQuote

trlExp
  = "{{" space* (trlMethod | trlFilter) space* "}}"
  
trlMethod
  = "trl" "(" space* trlString space* ")"
  
trlFilter
  = trlString (space* "|" space* filterName)*
 
filterName
  = namedToken (":" namedToken)*
  
  
}