Objc <: Base {

// OVERRIDES
TMLLocalizedString
  := Macro "(" StringObject ("," ListOf<Args, ",">)* ")" ";"*

NullLiteral
  := "nil"
  | "NULL"

// END OF OVERRIDES
  
Macro
  = "TMLLocalizedString"
  | "TMLLocalizedAttributedString"
  | "TMLLocalizedDate"
  | "TMLLocalizedAttributedDate"


// TMLLocalized argument expression
Args
  = Literal | Object | MessageSend | CollectionAccess | Property | Variable

CollectionAccess
  = Property "[" Args "]"
  | Variable "[" Args "]"
  
Property
  = Variable ("." Variable)*

// Object  
Object
  = StringObject
  | NumberObject
  | Dict
  | Array
  | LiteralWrap
  
// Literal Wrap @(WHATEVER)
LiteralWrap
  = "@(" Variable ")"
  
// Number Object
NumberObject
  = "@" NumberLiteral

// String Object
StringObject
  = "@" StringLiteral ("@"? StringLiteral)*


// Dictionary
Dict
  = "@{" ListOf<DictEntry, ","> "}"
  
DictEntry
  = StringObject ":" DictValue
  
DictValue
  = Args


// Array
Array
  = "@[" ListOf<ArrayEntry, ","> "]"

ArrayEntry
  = Args


// Message Send
MessageSend
  = NoArgumentMessageSend
  | ArgumentMessageSend
  
NoArgumentMessageSend
  = "[" Receiver NoArgumentMessage "]"

ArgumentMessageSend
  = "[" Receiver (ArgumentMessage Args)+ "]"
  
Receiver
  = MessageComponent
  
NoArgumentMessage
  = MessageComponent
  
ArgumentMessage
  = MessageComponent ":"

MessageComponent
  = #(messageComponentChar+)

messageComponentChar
  = ~("["|"]"|":"|space) any

}