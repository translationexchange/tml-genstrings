Objc <: Base {

// OVERRIDES
TMLLocalizedString
  := Macro "(" StringObject ("," ArgExp)* ")" ";"*

NullLiteral
  := "nil"
  | "NULL"
  
BooleanLiteral
  += "YES" | "NO"
  
StringLiteral
  := QuotedStringLiteral<"\"", stringNonLiteral>
  
Arg
  := Literal 
  | CollectionAccess
  | Object
  | MessageSend  
  | Property
  | Variable

// END OF OVERRIDES
  
Macro
  := "TMLLocalizedString"
  | "TMLLocalizedAttributedString"
  | "TMLLocalizedDate"
  | "TMLLocalizedAttributedDate"

CollectionAccess
  = Property "[" ArgExp "]"
  | Variable "[" ArgExp "]"

// Object  
Object
  = StringObject
  | NumberObject
  | Dict
  | Array
  | LiteralWrap
  
// Literal Wrap @(WHATEVER)
LiteralWrap
  = "@(" Literal ")"
  | "@(" Variable ")"
  
// Number Object
NumberObject
  = "@" NumberLiteral

// String Object
StringObject
  = "@" StringLiteral ("@"? StringLiteral)*


// Dictionary
Dict
  = "@{" ListOf<DictEntry, ","> "}"
  
DictEntry
  = StringObject ":" DictValue
  
DictValue
  = ArgExp


// Array
Array
  = "@[" ListOf<ArrayEntry, ","> "]"

ArrayEntry
  = ArgExp


// Message Send
MessageSend
  = NoArgumentMessageSend
  | ArgumentMessageSend
  
NoArgumentMessageSend
  = "[" Receiver NoArgumentMessage "]"

ArgumentMessageSend
  = "[" Receiver (ArgumentMessage ArgExp)+ "]"
  
Receiver
  = MessageComponent
  
NoArgumentMessage
  = MessageComponent
  
ArgumentMessage
  = MessageComponent ":"

MessageComponent
  = #(messageComponentChar+)

messageComponentChar
  = ~("["|"]"|":"|space) any

}