HTML <: Base {

// OVERRIDES
comment
  := startComment (~endComment any)* endComment
  
startComment
  = "<" space * "!" space* "--" space*

endComment
  = "--" space* ">"
  
Arg
  := Method | Object | Property | Variable | Literal
  
stringNonLiteral
  := "{{" | "}}"
  
TMLLocalizedString
  := XMLTagWithContent<"tml-tr", TMLLocalizedTagContent> --tmlTag
  | XMLTagWithAttribute<XMLTagName, TRLAttributeExp> --trlInAttr
  | XMLTagWithAttribute<XMLTagName, TMLAttributeExp> --tmlInAttr
  | XMLTagWithAttributeAndContent<XMLTagName, "tml-tr", TMLLocalizedTagContent> --markedTag
  | XMLTagWithContent<XMLTagName, TRLExp> --trlInTag

// END OF OVERRIDES

TMLLocalizedTagContent
  = TRLExp --trl
  | (TagStringChar | NonTMLTag)+
  
TRLAttributeExp
  = XMLTagAttributeName "=" QuotedTRLExp
  
TMLAttributeExp
  = "tml-tr" "=" (QuotedTRLExp | StringLiteral)
  
NonTMLAttribute
  = ~"tml-tr" XMLTagAttributeExp

NonTMLTag
  = "<" XMLTagName NonTMLAttribute* ">" TMLLocalizedTagContent XMLCloseAnyTag
  
TagStringChar
  = ~("<" | CodeMarkup) any
  
// Special Markup
CodeMarkup
  = "{{" (~"}}" any)* "}}"

// TRL
QuotedTRLExp
  = "\"" TRLExp "\""
  | "'" TRLExp "'"

TRLExp
  = "{{" (TRLMethod | TRLFilter) "}}"
  
TRLMethod
  = "trl" "(" StringLiteral ")"
  
TRLFilter
  = StringLiteral "|" "trl" (":" ArgExp)? ( "|" FilterName)*
 
FilterName
  = Variable
  
Object
  = Dict
  | Array
  
Dict
 = "{" ListOf<DictEntry, ","> "}"
  
DictEntry
  = (StringLiteral | Variable) ":" ArgExp
  
Array
  = "[" ListOf<ArgExp, ","> "]"

}